#!/usr/bin/env perl
use v5.10.1;
use strict;
use warnings;
# PODNAME: p
# ABSTRACT: command-line read-write access to http://p.hashbang.ca
# VERSION

use WWW::Hashbang::Pastebin::Client;
use Getopt::Long;
use Pod::Usage;

my %opts = ( url => 'http://p.hashbang.ca' );
GetOptions( \%opts,
    'help|?',
    'version',
    'txt',
    'url=s',
);

pod2usage(
    -verbose => 2,
) if $opts{help};

=head1 SYNOPSIS

    p < file
    ps aux | p
    p --help

=head1 DESCRIPTION

B<p> is a simple command line program to write to (and read from)
the pastebin L<http://p.hashbang.ca>, which runs L<WWW::Hashbang::Pastebin>.

=head2 Writing

Provide input on stdin to C<p> to paste it to the pastebin. Provide the
B<--lang> option to specify syntax highlighting to apply.

    ps aux | sprunge
    sprunge --lang pl < file.pl

=head2 Reading

Run C<p> with a URL or paste ID to output the text to stdout.

    p H2gc | less

=head1 OPTIONS

=over 4

=item B<--help>, -h, -?

Opens this man page and exits.

=item B<--version>

Prints the version of this program and supporting libraries.

=item B<--url>

Set the URL to a different site running L<WWW::Hashbang::Pastebin>.

=back

=cut

if (delete $opts{version}) {
    my $this = __PACKAGE__;
    my $this_ver = __PACKAGE__->VERSION();
    say "$this version $this_ver" and exit;
}

my $client = WWW::Hashbang::Pastebin::Client->new(url => $opts{url});

if (@ARGV) {    # READ
    my $paste_id = shift(@ARGV);
    $paste_id =~ s{\.$}{};
    say $client->get($paste_id);
}
else {          # WRITE
    my $text = do { local $/; <STDIN> };
    say $client->paste(paste => $text);
}
